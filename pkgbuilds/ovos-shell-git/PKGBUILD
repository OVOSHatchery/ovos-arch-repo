# Maintainer: Aditya Mehra <aix (dot) m (at) outlook (dot) com>

pkgname=ovos-shell
pkgver=r59.6fa384a
pkgrel=1
pkgdesc="OpenVoiceOS Shell for Wayland and EGLFS platforms"
arch=("aarch64")
url="https://github.com/OpenVoiceOS/ovos-shell/"
license=('APACHE')
depends=('qt5-base' 'qt5-websockets' 'qt5-quickcontrols2' 'qt5-multimedia' 'qt5-webengine' 'qt5-declarative' 'qt5-webview'
         'gst-libav' 'gst-plugins-bad' 'gst-plugins-base' 'gst-plugins-ugly' 'gst-plugins-good' 'lottie-qml' 'mycroft-gui')
makedepends=('git' 'cmake' 'extra-cmake-modules' 'plasma-framework' 'kio' 'kio-extras')
source=("git+https://github.com/OpenVoiceOS/$pkgname")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd $pkgname
  mkdir -p build && cd build
  cmake .. -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_BUILD_TYPE=Release -DKDE_INSTALL_LIBDIR=lib -DKDE_INSTALL_USE_QT_SYS_PATHS=ON
  make
}

package() {
  cd $pkgname/build
  make DESTDIR="$pkgdir/" install
}
